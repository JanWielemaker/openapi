#!/usr/bin/env swipl

:- use_module(library(main)).
:- use_module(library(apply)).
:- use_module(library(lists)).
:- use_module(library(option)).
:- use_module(library(openapi)).

:- initialization(main, main).

main(Argv) :-
    argv_options(Argv, Files, Options),
    maplist(generate(Options), Files).

generate(Options, File) :-
    gen_client(File, Options),
    gen_server(File, Options).

gen_client(File, Options) :-
    option(client(Client), Options), !,
    output_options(client(Client), File, Out),
    append(Options, Out, GenOptions),
    openapi_doc(File, client, GenOptions).

gen_server(File, Options) :-
    option(server(Server), Options), !,
    output_options(server(Server), File, Out),
    append(Options, Out, GenOptions),
    openapi_doc(File, server, GenOptions).

output_options(client(-), _, []) :-
    !.
output_options(server(-), _, []) :-
    !.
output_options(client(true), From, [file(Out)]) :-
    !,
    file_name_extension(Base, _, From),
    atomic_list_concat([Base, '_client.pl'], Out).
output_options(server(true), From, [file(Out)]) :-
    !,
    file_name_extension(Base, _, From),
    atomic_list_concat([Base, '_server.pl'], Out).
output_options(client(File), _, [file(File)]).
output_options(server(File), _, [file(File)]).
